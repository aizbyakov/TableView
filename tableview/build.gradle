apply plugin: 'com.android.library'

def libName = 'tableview'

android {
    namespace "com.evrencoskun.tableview"

    defaultConfig {
        versionCode androidApplicationVersionCode
        versionName androidApplicationVersionName

        minSdk androidApplicationMinSdkVersion
        compileSdk androidApplicationCompileSdkVersion
        targetSdk androidApplicationTargetSdkVersion

        archivesBaseName = "$libName-$versionName"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility = javaSourceCompatibility
        targetCompatibility = javaTargetCompatibility
    }

    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation "androidx.recyclerview:recyclerview:1.3.2"

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation "androidx.test.espresso:espresso-core:3.6.1"
    androidTestImplementation "androidx.test.ext:junit:1.2.1"
    androidTestImplementation "androidx.test:rules:1.6.1"
}

tasks.register("cleanArtifacts", Delete) {
    delete fileTree(dir: "$artifactsDirectory" , include: "**/${libName}*.aar")
    delete fileTree(dir: "$artifactsDirectory" , include: "**/${libName}*sources.jar")
}

clean.finalizedBy(cleanArtifacts)

tasks.register("copyArtifacts") {
    def buildDirectory = layout.buildDirectory.getAsFile().get();
    def sourceRoot = "$buildDirectory/outputs/aar"

    doFirst {
        println "SOURCE: $sourceRoot"
        println "TARGET: $artifactsDirectory"
        println ""

        copy {
            from(fileTree(sourceRoot).files)
            into(artifactsDirectory)
            include("*.aar")

            eachFile {
                println "FILE: $it.relativeSourcePath -> $it.relativePath"
            }
        }
    }
}

build.finalizedBy(copyArtifacts)


tasks.register("generateSourcesJar") {
    def sourcesRoot = "$projectDir/src/main/java"
    def cmd = "jar cvf \"$artifactsDirectory/$archivesBaseName-sources.jar\" -C \"$sourcesRoot/\" com"

    doFirst {
        println "GENERATE SOURCES JAR"
        println "SOURCES: $sourcesRoot"
        println "CMD: $cmd"

        exec {
            commandLine 'cmd', '/c', cmd
        }
    }
}

copyArtifacts.finalizedBy(generateSourcesJar)
